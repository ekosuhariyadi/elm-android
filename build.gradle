group 'com.codangcoding'
version '1.0-0'

buildscript {

    ext {
        kotlin_version = '1.1.60'
        rxjava2_version = '2.1.6'
        rxrelay_version = '2.0.0'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'kotlin'

group = GROUP
version = VERSION_NAME

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// Create source/javadoc artifacts for publishing
apply from: "$rootDir/gradle/artifacts.gradle"

// Configure android-maven-gradle-plugin
install {
    repositories.mavenInstaller.pom.project {
        name POM_NAME
        description POM_DESCRIPTION
        packaging POM_PACKAGING
        url POM_URL

        licenses {
            license {
                name POM_LICENCE_NAME
                url POM_LICENCE_URL
                distribution POM_LICENCE_DIST
            }
        }

        scm {
            url POM_SCM_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
        }

        developers {
            developer {
                id 'EkoSuhariyadi'
                name 'Eko Suhariyadi'
            }
        }
    }
}

// Configure gradle-bintray-plugin (for publishing releases)
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    publish = true

    pkg {
        repo = REPO_NAME
        name = POM_NAME
        desc = POM_DESCRIPTION
        userOrg = REPO_USER
        licenses = ['Apache-2.0']
        labels = ['dagger2', 'android-arch-viewmodel']
        websiteUrl = REPO_URL
        issueTrackerUrl = REPO_ISSUE_URL
        vcsUrl = REPO_GIT_URL
        publicDownloadNumbers = true

        version {
            desc = POM_DESCRIPTION
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxrelay_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}